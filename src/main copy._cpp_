#include <Arduino.h>
#include <SoftwareSerial.h>

unsigned long startTime = millis();

#define CO2_RX_PIN 32 // Rx pin which the MHZ19 Tx pin is attached to
#define CO2_TX_PIN 33 // Tx pin which the MHZ19 Rx pin is attached to

SoftwareSerial co2Serial(CO2_RX_PIN, CO2_TX_PIN); // define MH-Z19 RX TX D3 (GPIO0) and D4 (GPIO2)



class CO2_sensor
{

private:
  SoftwareSerial *m_serial;
  HardwareSerial *m_monitor;
  bool m_verbose{false};
  int readCO2UART()
  {
    byte cmd[9] = {0xFF, 0x01, 0x86, 0x00, 0x00, 0x00, 0x00, 0x00, 0x79};
    char response[9];
    if (m_verbose)
    {
      m_monitor->println("Sending CO2 request...");
    }
    m_serial->write(cmd, 9); // request PPM CO2

    // clear the buffer
    memset(response, 0, 9);
    int i = 0;
    while (m_serial->available() == 0)
    {
      if (m_verbose)
      {
        m_monitor->print("Waiting for response ");
        m_monitor->print(i);
        m_monitor->println(" s");
      }
      delay(1000);
      i++;
    }
    if (m_serial->available() > 0)
    {
      m_serial->readBytes(response, 9);
    }
    // print out the response in hexa
    for (int i = 0; i < 9; i++)
    {
      if (m_verbose)
      {
        m_monitor->print(String(response[i], HEX));
        m_monitor->print("   ");
      }
    }
    if (m_verbose)
    {
      m_monitor->println("");
    }
    // checksum
    byte check = getCheckSum(response);
    if (response[8] != check)
    {
      if (m_verbose)
      {
        m_monitor->println("Checksum not OK!");
        m_monitor->print("Received: ");
        m_monitor->println(response[8]);
        m_monitor->print("Should be: ");
        m_monitor->println(check);
      }
    }
    // ppm
    int ppm_uart = 256 * (int)response[2] + response[3];
    if (m_verbose)
    {
      m_monitor->print("UART CO2 PPM: ");
      m_monitor->println(ppm_uart);
    }
    // temp
    byte temp = response[4] - 40;
    if (m_verbose)
    {
      m_monitor->print("Sensor Temperature: ");
      m_monitor->println(temp);
    }
    // status
    byte status = response[5];
    if (m_verbose)
    {
      m_monitor->print("Status: ");
      m_monitor->println(status);
    }
    if (status == 0x40)
    {
      if (m_verbose)
      {
        m_monitor->println("Status OK");
      }
    }
    return ppm_uart;
  }

  byte getCheckSum(char *packet)
  {
    byte i;
    unsigned char checksum = 0;
    for (i = 1; i < 8; i++)
    {
      checksum += packet[i];
    }
    checksum = 0xff - checksum;
    checksum += 1;
    return checksum;
  }

public:
  void begin(SoftwareSerial &i_serial, HardwareSerial &i_monitor, bool i_verbose = false)
  {
    m_serial = &i_serial;
    m_monitor = &i_monitor;
    m_verbose = i_verbose;
  }
  int read()
  {
    return readCO2UART();
  }
};

CO2_sensor myco2sensor;

void setup()
{
  Serial.begin(115200);
  co2Serial.begin(9600);
  myco2sensor.begin(co2Serial, Serial,true);
}

void loop()
{

  Serial.println("------------------------------");
  Serial.print("Time from start: ");
  Serial.print((millis() - startTime) / 1000);
  Serial.println(" s");
  int ppm_uart = myco2sensor.read();
  Serial.print("------>  " );
  Serial.println(ppm_uart);
  Serial.println();
  delay(15000);
}
